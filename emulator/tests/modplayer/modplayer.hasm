; module structure
.const mp_chn_0        0x00
.const mp_file         0x04
.const mp_curr_pat     0x08
.const mp_pat_ptr      0x0c
.const mp_sng_len      0x10
.const mp_restart_at   0x14
.const mp_seq_ptr      0x18
.const mp_num_pats     0x1c
.const mp_num_chn      0x20
.const mp_max_samples  0x24

; playing vars
.const mp_pat_pos      0x28
.const mp_delay        0x2c
.const mp_spd          0x30

;.const mp_file        0x34
;.const mp_curr_pat    0x38
;.const mp_pat_ptr     0x3c
;.const mp_chn_0       0x40
;.const mp_file        0x44
;.const mp_curr_pat    0x48
;.const mp_pat_ptr     0x4c

.const mp_samples     0x100

; sample structure
.const mps_sizeof     0x30

.const mps_smpptr     0x00
.const mps_len        0x04
.const mps_finetune   0x08
.const mps_vol        0x0C
.const mps_loopstart  0x10
.const mps_replen     0x18

; constants
.const mp_magic_pal 7093789 ; "magic number" for PAL timing, period/freqency divisor

mp_dmsg: .db "dec: %d", 10, 0
mp_xmsg: .db "hex: 0x%x", 10, 0
mp_newline: .db 10, 0

; parse a sapmle
;   a -> pointer to sample structure to write to
;   j -> file pointer 

mp_sname_msg:   .db "sample name: %s", 10, 0
mp_ftune_tab:   .db 0 +1 +2 +3 +4 +5 +6 +7 -8 -7 -6 -5 -4 -3 -2 -1
mps_parsed_msg: .db "mps_len: %d mps_finetune: %d mps_vol: %d mps_loopstart: %d mps_replen: %d", 10, 0

modplayer_parse_sample:
	set.l push x

	set.l push, j
	set.l push, mp_sname_msg
	sys SYS_PRINT

	; skip 22 char sample name
	add j, 22

	; 2 bytes sample length (/2)
	set.l x, mps_len
	add x, a

	set.b [1+x], [j]
	set.b [x], [1+j]

	mul [mps_len+a], 2 ; length is divided by 2 in file
	add j, 2

	; 1 byte fine tune
	set.b x, [j]
	mul x, 4
	set.l [mps_finetune+a], [mp_ftune_tab+x]
	add j, 1

	; 1 byte sample volume
	set.l [mps_vol+a], [j]
	mul [mps_vol+a], 4 ; 0-64 -> 0-255
	ifg  [mps_vol+a], 255
		set.l [mps_vol+a], 255
	add j, 1

	; 2 byte loop start
	set.w [mps_loopstart+a], [j]

	set.l x, mps_loopstart
	add x, a

	set.b [1+x], [j]
	set.b [x], [1+j]

	mul [mps_loopstart+a], 2 
	add j, 2
	
	; 2 byte loop length
	set.w [mps_replen+a], [j]

	set.l x, mps_replen
	add x, a

	set.b [1+x], [j]
	set.b [x], [1+j]

	mul [mps_replen+a], 2 
	add j, 2

	; print result
	set.l push, [mps_replen+a]
	set.l push, [mps_loopstart+a]
	set.l push, [mps_vol+a]
	set.l push, [mps_finetune+a]
	set.l push, [mps_len+a]

	set.l push, mps_parsed_msg
	sys SYS_PRINT

	set.l x, pop

	set.l pc, pop

; init the player
;   a -> mod player object pointer
;   b -> mod file pointer
;   c -> base channel to use

mpi_msg:        .db "song name: %s", 10, 0
mpi_parsed_msg: .db "song length: %d, restart: %d, num pats: %d", 10, 0
mpi_vstr:       .db "string: %c%c%c%c", 10, 0

modplayer_init:
	set.l [mp_file+a], b
	set.l [mp_chn_0+a], c

	; print song name
	set.l push, [mp_file+a]
	set.l push, mpi_msg

	sys SYS_PRINT

	; store "file pointer" in j
	set.l j, [mp_file+a]

	; skip 20 char song name
	add j, 20

	; save object pointer in x
	set.l x, a
	
	; put sample pointer in a
	set.l a, mp_samples
	add a, x
	
	; Set the max number of instruments
	; TODO check if it's a 31 instrument mod or not
	set.l [mp_max_samples+x], 31
	
	; loop through and parse the sample headers
	set.l i, 0

	parse_sample_loop:
		jsr modplayer_parse_sample
		
		add a, mps_sizeof
		
		add i, 1
		ifn i, [mp_max_samples+x]
			set.l pc, parse_sample_loop

	; 1 byte song length
	set.b [mp_sng_len+x], [j]
	add j, 1

	; 1 byte song repeat
	set.b [mp_restart_at+x], [j]
	add j, 1

	; 128 byte song sequences
	set.l [mp_seq_ptr+x], j

	; number of patterns is the highest pattern stored + 1
	set.l i, 0
	calc_num_pats_loop:
		set.b y, [j]

		ifg y, [mp_num_pats+x]
			set.l [mp_num_pats+x], y
	
		add j, 1
		add i, 1
		ifn i, 128
			set.l pc, calc_num_pats_loop

	add [mp_num_pats+x], 1	
	
	set.l push, [mp_num_pats+x]
	set.l push, [mp_restart_at+x]
	set.l push, [mp_sng_len+x]
	set.l push, mpi_parsed_msg
	sys SYS_PRINT

	; "M.K." etc.
	set.l push, [3+j]
	set.l push, [2+j]
	set.l push, [1+j]
	set.l push, [j]
	set.l push, mpi_vstr
	sys SYS_PRINT

	add j, 4

	; Save a pointer to the patterns
	set.l [mp_pat_ptr+x], j

	; Set number of channels to 4 for now
	set.l [mp_num_chn+x], 4

	; Skip the patterns
	set.l y, 4             ; 4 bytes per note
	mul y, [mp_num_chn+x]  ; channels * notesize bytes per track
	mul y, 64              ; 64 tracks per pattern
	mul y, [mp_num_pats+x] ; skip mp_num_pats patterns

	add j, y

	; Get the sample pointers
	set.l i, 0

	; A pointer to the samples structure parsed earlier
	set.l y, mp_samples
	add y, x

	parse_sample_ptr_loop:
		ife [mps_len+y], 0
			set.l pc, parse_sample_skip

		set.l [mps_smpptr+y], j
		add j, [mps_len+y]

		set.l push, [mps_smpptr+y]
		set.l push, mp_xmsg
		sys SYS_PRINT
		
		parse_sample_skip:
		add y, mps_sizeof

		add i, 1
		ifn i, [mp_max_samples+x]
			set.l pc, parse_sample_ptr_loop

	; set default playback speed to 6
	set.l [mp_spd+x], 6

	set.l pc, pop

; print a note
;   b -> instrument number
;   c -> order/note
;   j -> effect param
;   y -> effect number

mp_order_tab:
.dd 1712,1616,1525,1440,1357,1281,1209,1141,1077,1017, 961, 907
.dd 856, 808, 762, 720, 678, 640, 604, 570, 538, 508, 480, 453
.dd 428, 404, 381, 360, 339, 320, 302, 285, 269, 254, 240, 226
.dd 214, 202, 190, 180, 170, 160, 151, 143, 135, 127, 120, 113
.dd 107, 101,  95,  90,  85,  80,  76,  71,  67,  64,  60,  57

mp_note_tab:
.db "C-0", 0, "C#0", 0, "D-0", 0, "D#0", 0, "E-0", 0, "F-0", 0, "F#0", 0, "G-0", 0, "G#0", 0, "A-0", 0, "A#0", 0, "B-0", 0
.db "C-1", 0, "C#1", 0, "D-1", 0, "D#1", 0, "E-1", 0, "F-1", 0, "F#1", 0, "G-1", 0, "G#1", 0, "A-1", 0, "A#1", 0, "B-1", 0
.db "C-2", 0, "C#2", 0, "D-2", 0, "D#2", 0, "E-2", 0, "F-2", 0, "F#2", 0, "G-2", 0, "G#2", 0, "A-2", 0, "A#2", 0, "B-2", 0
.db "C-3", 0, "C#3", 0, "D-3", 0, "D#3", 0, "E-3", 0, "F-3", 0, "F#3", 0, "G-3", 0, "G#3", 0, "A-3", 0, "A#3", 0, "B-3", 0
.db "C-4", 0, "C#4", 0, "D-4", 0, "D#4", 0, "E-4", 0, "F-4", 0, "F#4", 0, "G-4", 0, "G#4", 0, "A-4", 0, "A#4", 0, "B-4", 0, "---", 0

mp_effect_tab:
.db "0123456789ABCDEFGHIJKLMNOPQRSTUVXYZ"

mpp_note_msg: .db "%s %02x %c%02x | ", 0

modplayer_print_note:
	;set.l push, x
	set.l push, i

	set.l i, 0

	mpp_loop:
		ife [mp_order_tab+i], c
			set.l pc, mpp_break

		add i, 4
		ifn i, 240
			set.l pc, mpp_loop

	mpp_break:

	add i, mp_note_tab

	set.l push, j ; effect param
	set.l push, [mp_effect_tab+y] ; effect
	set.l push, b ; ins num
	set.l push, i
	
	set.l push, mpp_note_msg

	sys SYS_PRINT
	

	set.l i, pop
	;set.l x, pop
	set.l pc, pop

; trigger a note
;   a -> mod player object pointer
;   b -> instrument number
;   c -> order/note
;   j -> effect param
;   y -> effect number
;   z -> current channel pointer
;   ... wow

empty_note_msg: .db "--- - -- - --- | ", 0

modplayer_trigger_note:
	; if there's no note, return
	;ife c, 0
	;	set.l pc, mpt_no_note

	; otherwise continue
	set.l pc, mpt_is_note

	mpt_no_note:
	; print empty note string
	set.l push, empty_note_msg
	sys SYS_PRINT

	set.l pc, pop

	mpt_is_note:

	set.l push, x

	; get a pointer to the sample/instrument structure
	set.l x, mps_sizeof ; sizeof sample struct
	mul x, b            ; ... times instrument number
	add x, mp_samples   ; ... plus offset of the samples into the mp structure
	add x, a            ; ... plus the object location -> x contains the sample location

	; set the sample pointer
	set.l [APU_O_SPTR+z], [mps_smpptr+x]

	; reset player position
	set.l [APU_O_POS+z], 0

	; set the loop start
	set.l [APU_O_LSTART+z], [mps_loopstart+x]
	
	; set the end pos
	set.l [APU_O_LEND+z], [mps_loopstart+x]
	add [APU_O_LEND+z], [mps_replen+x]

	; enable looping if replen is bigger than 2 (1 in the file)
	set.l [APU_O_FLAGS+x], 0
	ifg [mps_replen+x], 2
		set.l [APU_O_FLAGS+x], APU_FLAG_LOOP

	; set the volume
	set.b [APU_O_VOL+z], [mps_vol+x]

	; set pan (mono for now)
	set.b [APU_O_PAN+z], 128

	; set playback frequency
	;   7093789.2 / (period * 2)

	set.l push, b
	set.l push, c

	mul c, 2
	set.l b, mp_magic_pal
	div b, c

	set.l [APU_O_FREQ+z], b

	;set.l push, b
	;set.l push, mp_dmsg
	;sys SYS_PRINT

	set.l c, pop
	set.l b, pop

	set.l x, pop

	jsr modplayer_print_note

	set.l pc, pop

; update the player
;   a -> mod player object pointer

mpt_note_msg:    .db "note: %d %x %x %x", 10, 0
mpt_pattern_msg: .db "[%02x:%02x]", 10, 0

modplayer_tick:
	jsr push_all

	; Skip to current note. 
	; Calculate pattern offset and put in y

	; get current pattern number from sequence and put in j
	set.l j, [mp_seq_ptr+a]  ; pointer to sequence
	add j, [mp_curr_pat+a]   ; index into sequence
	set.l j, [j]             ; read pattern number from sequence

	; Whole patterns size
	set.l y, 64              ; 64 tracks per pattern
	
	; ..times current pattern
	mul y, j

	; ...plus the current pos in the pattern
	add y, [mp_pat_pos+a]

	; ... times track size
	mul y, 4               ; 4 bytes per note
	mul y, [mp_num_chn+a]  ; channels * notesize bytes per track

	; Offset into pattern pointer to get to current note data
	set.l x, [mp_pat_ptr+a]
	add x, y

	; A pointer to the first hardware channel to use in z
	set.l z, [mp_chn_0+a]

	; Loop through the channels
	set.l i, 0
	
	mp_channel_loop:
		;set.b push, [x]
		;set.l push mp_xmsg
		;sys SYS_PRINT
		;set.b push, [1+x]
		;set.l push mp_xmsg
		;sys SYS_PRINT
		;set.b push, [2+x]
		;set.l push mp_xmsg
		;sys SYS_PRINT
		;set.b push, [3+x]
		;set.l push mp_xmsg
		;sys SYS_PRINT

		; 2 bytes instrument / period
		; highest 4 bits in LE 16b word, highest nibble in instrument number -> b
		set.b b, [x]
		and b, 0xf0

		; mask out so that only the 4 lowest bits remain, and shift them up to highest
		; of the 12 bit word that is the period
		set.b c, [x]
		and c, 0xf
		shl c, 4
		add x, 1

		; read lower 8 bits of word into y and or together with
		; the higher 4 bits in c
		; c now contains order/note
		set.b y, [x]
		bor c, y

		add x, 1

		; 1 bytes instrument / effect
		; highest 4 bits is lower nibble in instrument number
		; grab to y, shift down and or together with the higher 
		; instrument nibble -> b now contains instrument
		set.b y, [x]
		and y, 0xf0
		shr y, 4
		bor b, y
		
		; read lower nibble -> y now contains effect
		set.b y, [x]
		and y, 0xf 
		
		add x, 1

		; read effect parameter into j
		set.b j, [x]
		add x, 1

		; print parsed note
		;set.l push, j ; effect param
		;set.l push, y ; effect number
		;set.l push, b ; instrument number
		;set.l push, c ; order/note
		;set.l push, mpt_note_msg
		;sys SYS_PRINT
		
		; trigger the note if delay is 0
		ife [mp_delay+a], 0
			jsr modplayer_trigger_note

		; step to the next hardware channel
		add z, APU_CHN_SIZE
						

		add i, 1
		ifn i, [mp_num_chn+a]
			set.l pc, mp_channel_loop

	; if the delay is not 0, just decrease the timer
	ifn [mp_delay+a], 0
		set.l pc, mpt_dec_timer

	; show pattern
	set.l push, [mp_pat_pos+a]
	set.l push, [mp_curr_pat+a]
	set.l push, mpt_pattern_msg
	sys SYS_PRINT

	; reset note delay timer to speed
	set.l [mp_delay+a], [mp_spd+a]
	
	; if pos is 63, jump to next pattern
	ife [mp_pat_pos+a], 63
		set.l pc, mpt_next_ptn
	
	; step to next pos
	add [mp_pat_pos+a], 1

	set.l pc, mpt_return

	mpt_next_ptn:
		; reset pos to 0
		set.l [mp_pat_pos+a], 0

		set.l b, [mp_num_pats+a]
		sub b, 1

		; if we're at the last pattern, loop to loop point
		ife b, [mp_curr_pat+a]
			set.l pc, mpt_loop_song

		add [mp_curr_pat+a], 1

		set.l pc, mpt_return

	mpt_loop_song:
		set.l [mp_curr_pat+a], [mp_restart_at+a]
		set.l pc, mpt_return
	
	mpt_dec_timer:
		ifn [mp_delay+a], 0
			sub [mp_delay+a], 1

	mpt_return:
		jsr pop_all
		set.l pc, pop
